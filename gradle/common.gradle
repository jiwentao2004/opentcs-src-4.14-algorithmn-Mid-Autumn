//
// This file is to be applied to every subproject.
//

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'checkstyle'
apply plugin: 'org.kordamp.gradle.stats'

apply from: "${rootDir}/gradle/versioning.gradle"

group = 'org.opentcs'

sourceCompatibility = '1.8'
archivesBaseName = name.toLowerCase()

// Attributes for the AsciiDoc documentation to include code from source files
ext.javaSrcDir = sourceSets.main.java.srcDirs[0]
ext.javaClassesDir = sourceSets.main.output.classesDirs
ext.testSrcDir = sourceSets.test.java.srcDirs[0]

repositories {
  jcenter()
}

dependencies {
  compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.21'
  compileOnly group: 'org.slf4j', name: 'slf4j-api', version: '1.7.21', classifier: 'javadoc'

  compileOnly group: 'javax.inject', name: 'javax.inject', version: '1'

  compileOnly group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.1'
  testCompileOnly group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.1'

  // For JUnit 4.x tests
  testCompileOnly group: 'junit', name: 'junit', version: '4.12'
  testRuntimeOnly group: 'org.junit.vintage', name: 'junit-vintage-engine', version: '5.3.2'
  // For JUnit 5 tests
  testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.3.2'
  testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: '5.3.2'
  testCompileOnly group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.3.2', classifier: 'javadoc'
  testCompileOnly group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: '5.3.2', classifier: 'javadoc'
  testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.3.2'

  testCompile group: 'org.hamcrest', name: 'hamcrest-core', version: '1.3'
  testCompileOnly group: 'org.hamcrest', name: 'hamcrest-core', version: '1.3', classifier: 'sources'
  testCompile group: 'org.hamcrest', name: 'hamcrest-library', version: '1.3'
  testCompileOnly group: 'org.hamcrest', name: 'hamcrest-library', version: '1.3', classifier: 'sources'
  testCompile group: 'org.mockito', name: 'mockito-core', version: '2.8.47'
  testCompileOnly group: 'org.mockito', name: 'mockito-core', version: '2.8.47', classifier: 'sources'
  
  testRuntime group: 'org.slf4j', name: 'slf4j-jdk14', version: '1.7.21'
}

compileJava {
  options.compilerArgs << "-Xlint:all"
  options.compilerArgs << "-Xlint:-serial"
}

compileTestJava {
  options.compilerArgs << "-Xlint:all"
  options.compilerArgs << "-Xlint:-serial"
}

checkstyle {
  toolVersion = '8.16'
  configFile = rootProject.file("config/checkstyle/checkstyle.xml")
  configProperties = [suppressions_file:rootDir.absolutePath + "/config/checkstyle/suppressions.xml"]
  showViolations = false
}

project.afterEvaluate { project ->
  project.tasks.withType(Checkstyle) {
    reports {
      html.stylesheet resources.text.fromFile(rootProject.file("config/checkstyle/checkstyle-noframes-severity-sorted.xsl"))
    }
  }
}

task sourcesJar(type: Jar, dependsOn: classes, description: 'Creates a jar from the source files.') {
  classifier = 'sources'
  from sourceSets.main.allSource
}

test {
  useJUnitPlatform()
  ignoreFailures = true
  systemProperties.put("java.awt.headless", "true")
}

artifacts {
  archives jar
  archives sourcesJar
}

configure(install.repositories.mavenInstaller) {
  pom.project {
    groupId = project.group
    artifactId = project.name.toLowerCase()
    version = project.version
  }
}

task createFolders(description: 'Creates the source folders if they do not exist.') doLast {
  sourceSets*.allSource*.srcDirs*.each { File srcDir ->
    if (!srcDir.isDirectory()) {
      println "Creating source folder: ${srcDir}"
      srcDir.mkdirs()
    }
  }
}
